start: bot_def
bot_def: "bot" NAME "{" state_def+ "}"
state_def: "state" NAME "{" instruction+ "}"

instruction: say_cmd
           | listen_cmd
           | goto_cmd
           | exit_cmd
           | process_cmd
           | set_cmd
           | call_cmd
           | if_cmd
           | sql_cmd

say_cmd: "say" STRING
listen_cmd: "listen" (VAR_NAME)?
goto_cmd: "goto" NAME
exit_cmd: "exit"
set_cmd: "set" VAR_NAME "=" value
call_cmd: "call" NAME "(" (value ("," value)*)? ")" "as" VAR_NAME
if_cmd: "if" value COMP_OP value "goto" NAME
sql_cmd: "sql" STRING ("as" VAR_NAME)?

process_cmd: "process" "{" case_rule+ default_rule? "}"
case_rule: "user_intent" STRING "=>" action
default_rule: "default" "=>" action

action: goto_cmd | say_cmd | exit_cmd

value: STRING | INT | VAR_NAME
VAR_NAME: "$" /[a-zA-Z0-9_]+/
COMP_OP: "==" | "!=" | ">" | "<"

%import common.CNAME -> NAME
%import common.ESCAPED_STRING -> STRING
%import common.INT
%import common.WS
%import common.CPP_COMMENT

%ignore WS
%ignore CPP_COMMENT



